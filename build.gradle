buildscript {
    ext {
        springBootVersion = '2.1.0.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'

group = ''
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

checkstyle {
	toolVersion "8.10"
}

project.ext {
    cucumberVersion = '4.2.0'
    seleniumVersion = '3.141.59'
}

dependencies {
    developmentOnly('org.springframework.boot:spring-boot-devtools')
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation('org.springframework.boot:spring-boot-starter-thymeleaf')
    implementation('org.springframework.boot:spring-boot-starter-web')
    runtimeOnly('com.h2database:h2')
    runtimeOnly('org.postgresql:postgresql')
    testCompile('junit:junit:4.12')
    testCompile('io.cucumber:cucumber-java:' + cucumberVersion)
    testCompile('io.cucumber:cucumber-junit:' + cucumberVersion)
    testCompile('io.cucumber:cucumber-spring:' + cucumberVersion)
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    compile('org.seleniumhq.selenium:selenium-api:' + seleniumVersion)
    compile('org.seleniumhq.selenium:selenium-java:' + seleniumVersion)
    compile('org.seleniumhq.selenium:htmlunit-driver:2.33.3')
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
                fileTree(dir: it, 
                    exclude: ['ohtutips/OhtuTipsApplication*',
                    'ohtutips/controller/**',
                    'ohtutips/repository/**'])
            })
    }
}

check.dependsOn jacocoTestReport

bootRun {
    String activeProfile =  System.properties['spring.profiles.active']
    String confLoc = System.properties['spring.config.location']
    systemProperty "spring.profiles.active", activeProfile
    systemProperty "spring.config.location", "file:$confLoc"
}
